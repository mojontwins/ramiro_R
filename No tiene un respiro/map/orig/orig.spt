# El Vampiro Ramiro no tiene un respiro
# Copyleft 2013 by The Mojon Twins.
# Churrera 4.6

# flags:
# 1	- Number of coins
# 2 - Number of talisman pieces
# 3 - Hechizo postizo
# 4 - Encantamiento del pimiento
# 6 - Onscreen check flag
# 7 - Tesoro del moro
# 8 - Go see
# 9 - trampa
# 10 - talisman 1
# 11 - talisman 2
# 12 - talisman 3
# 13 - talisman 4
# 14 - Canija gone

ENTERING GAME
	IF TRUE
	THEN
		#SET FLAG 3 = 1 ##
		#SET FLAG 8 = 1 ##
		#SET FLAG 9 = 1 ##
		#SET FLAG 10 = 1 ##
		#SET FLAG 11 = 1 ##
		#SET FLAG 2 = 4 ##
		#SET FLAG 14 = 1 ##
		#ENABLE_KILL_SLOWLY ###
		
		DISABLE_KILL_SLOWLY
		DISABLE_TYPE_6
	END
END

ENTERING ANY
	IF FLAG 2 = 4
	IF FLAG 4 = 0
	IF FLAG 8 = 0
	THEN
		SET FLAG 8 = 1
		#TEXT ___GO_SEE_TWITCH_THE_WITCH____
		TEXT __VE_A_VER_A_LA_BRUJA_MARUJA__
	END
END

PLAYER_GETS_COIN
	#IF FLAG 1 = 10
	#THEN
	#	INC LIFE 5
	#END

	IF TRUE
	THEN
		INC LIFE 1
	END
	
	IF FLAG 1 = 20
	THEN
		#INC LIFE 5
		SOUND 8
		DISABLE_TYPE_6
		HIDE_COINS
		REDRAW
		#TEXT _______TRAP_DEACTIVATED_______
		TEXT ______TRAMPA_DESACTIVADA______
	END
END

# Pantalla inicial

ENTERING SCREEN 7
	IF FLAG 14 = 0
	THEN
		#TEXT ___I_FORGOT_HER_DEATHDAY_!!___
		TEXT ___OLVIDE_SU_CUMPLEMUERTES!___
		SET TILE (4, 4) = 27
		SET_FIRE_ZONE 80, 24, 91, 71
		SET FLAG 6 = 0
	END
	
	IF TRUE
	THEN
		SET TILE (2, 3) = 30
		SET TILE (2, 4) = 31
	END
END

PRESS_FIRE AT SCREEN 7
	IF FLAG 7 = 0
	IF FLAG 6 = 0
	IF FLAG 14 = 0
	THEN
		SET FLAG 6 = 1
		#TEXT _____I_WANT_MY_PRESENT_!!_____
		TEXT ____QUIERO_MI_PUTO_REGALO!____
		SHOW
		SOUND 10
	END
	
	IF FLAG 7 = 1
	IF FLAG 14 = 0
	THEN
		#TEXT ______THANKSIES_DADDY!________
		TEXT ________GRACIAS_PAPI!_________
		SHOW
		SOUND 11
		SOUND 7
		SET TILE (4, 4) = 0
		SET_FIRE_ZONE 24, 48, 48, 79
		SET FLAG 14 = 1
	END IF
	
	IF FLAG 14 = 1
	IF PLAYER_TOUCHES 2, 4
	THEN
		SOUND 8
		WIN GAME
	END IF
END

# Limítrofes

ENTERING SCREEN 6
	IF FLAG 3 = 0
	THEN
		#TEXT WONT_GO_OUT!_NEED_REARM_CHARM!
		TEXT NO_VOY_SIN_EL_HECHIZO_POSTIZO!
	END
END

ENTERING SCREEN 8
	IF TRUE
	THEN
		SET TILE (8, 5) = 26
		SET TILE (7, 5) = 29
		SET_FIRE_ZONE 96, 80, 127, 95
	END

	IF FLAG 3 = 0
	THEN
		#TEXT WONT_GO_OUT!_NEED_REARM_CHARM!
		TEXT NO_VOY_SIN_EL_HECHIZO_POSTIZO!
	END
END

# Hechizo postizo y encantamiento del pimiento

PRESS_FIRE AT SCREEN 8
	IF FLAG 3 = 0
	IF PLAYER_TOUCHES 6,5
	THEN
		SET FLAG 3 = 1
		ENABLE_KILL_SLOWLY
		EXTERN 1
		SOUND 6
		EXTERN 2
		REDRAW
		SET TILE (8, 5) = 26
		SET TILE (7, 5) = 29
	END
	
	IF FLAG 2 = 4
	IF FLAG 4 = 0
	THEN
		# Encantamiento del pimiento
		SET FLAG 4 = 1
		EXTERN 3
		REDRAW
		SET TILE (8, 5) = 26
		SET TILE (7, 5) = 29
	END
END

# Cripta de Juanillo el Colmillo

ENTERING SCREEN 11
	IF TRUE
	THEN
		SET TILE (10, 8) = 21
		#TEXT ____HWANG_THE_FANG'S_CRYPT____
		TEXT CRIPTA_DE_JUANILLO_EL_COLMILLO
		SET_FIRE_ZONE 160, 128, 175, 143
		SET FLAG 6 = 0
	END
END

PRESS_FIRE AT SCREEN 11
	IF PLAYER_TOUCHES 10, 8
	IF FLAG 6 = 0
	THEN
		#TEXT _______HWANG:_SUP_BRO?________
		TEXT ____JUANILLO:_OLA_KE_ASE?_____
		SHOW
		SOUND 11
		SET FLAG 6 = 1
	END
END

# Talisman #1 (FLAG 10)

ENTERING SCREEN 23
	IF FLAG 10 = 0
	THEN
		HIDE_COINS
		SET FLAG 6 = 0
		SET TILE (12, 4) = 17
		#TEXT ____ONE_TALISMAN_PORTION!!____
		TEXT ___UNA_PIEZA_DEL_TALISMAN!!___
		SET_FIRE_ZONE 192, 64, 207, 79
	END
END

PRESS_FIRE AT SCREEN 23
	IF FLAG 10 = 0
	IF FLAG 6 = 0
	IF PLAYER_TOUCHES 12, 4
	THEN
		SET FLAG 1 = 0
		SET FLAG 6 = 1
		SET FLAG 10 = 1
		INC FLAG 2, 1
		INC OBJECTS 1
		SHOW_COINS
		ENABLE_TYPE_6
		REDRAW
		SET TILE (14, 2) = 16
		SET TILE (12, 4) = 0
		#TEXT __________TRAPPED!!___________
		TEXT __________ATRAPADO!!__________
		SHOW
		SOUND 6
	END
END

# Cripta del Gigante Elegante

ENTERING SCREEN 13
	IF TRUE
	THEN
		SET TILE (12, 7) = 22
		SET TILE (12, 8) = 23
		#TEXT _____ELEGANT_GIANT'S_CRYPT_____
		TEXT _CRIPTA_DE_EL_GIGANTE_ELEGANTE_
		SET_FIRE_ZONE 192, 112, 207, 143
		SET FLAG 6 = 0
	END
END

PRESS_FIRE AT SCREEN 13
	IF PLAYER_IN_X 184, 215
	IF PLAYER_IN_Y 112, 143
	IF FLAG 6 = 0
	THEN
		#TEXT _GIANT:_VENDO_MOTO_SEMINUEVA__
		TEXT GIGANTE:_VENDO_MOTO_SEMINUEVA_
		SHOW
		SOUND 11
		SET FLAG 6 = 1
	END
END

# Talisman #2 (FLAG 11)

ENTERING SCREEN 27
	IF FLAG 11 = 0
	THEN
		HIDE_COINS
		SET FLAG 6 = 0
		SET TILE (3, 2) = 17
		#TEXT ____ONE_TALISMAN_PORTION!!____
		TEXT ___UNA_PIEZA_DEL_TALISMAN!!___
		SET_FIRE_ZONE 48, 32, 63, 47
	END
END

PRESS_FIRE AT SCREEN 27
	IF FLAG 11 = 0
	IF FLAG 6 = 0
	IF PLAYER_TOUCHES 3, 2
	THEN
		SET FLAG 1 = 0
		SET FLAG 6 = 1
		SET FLAG 11 = 1
		INC FLAG 2, 1
		INC OBJECTS 1
		SHOW_COINS
		ENABLE_TYPE_6
		REDRAW
		SET TILE (14, 8) = 16
		SET TILE (3, 2) = 0
		#TEXT __________TRAPPED!!___________
		TEXT __________ATRAPADO!!__________
		SHOW
		SOUND 6
	END
END

# It's a trap!

ENTERING SCREEN 29
	IF FLAG 9 = 0
	THEN
		HIDE_COINS
		SET FLAG 6 = 0
		SET_FIRE_ZONE 48, 16, 79, 143
	END
END

PRESS_FIRE AT SCREEN 29
	IF FLAG 9 = 0
	IF FLAG 6 = 0
	IF PLAYER_IN_X 48, 79
	IF PLAYER_IN_Y 16, 143
	THEN
		SET FLAG 1 = 0
		SET FLAG 6 = 1
		SET FLAG 9 = 1
		SHOW_COINS
		ENABLE_TYPE_6
		REDRAW
		SET TILE (0, 6) = 16
		#TEXT ____OH_NO!!_IT'S_A_TRAP!!_____
		TEXT ___OH_NO!!_ES_UNA_TDUAMPA!!___
		SHOW
		SOUND 6
	END
END

# Cripta del enano marrano

ENTERING SCREEN 4
	IF TRUE
	THEN
		SET TILE (10, 4) = 24
		#TEXT ______BARF_DWARF'S_CRYPT_______
		TEXT ___CRIPTA_DEL_ENANO_MARRANO____
		SET_FIRE_ZONE 160, 64, 175, 79
		SET FLAG 6 = 0
	END
END

PRESS_FIRE AT SCREEN 4
	IF PLAYER_TOUCHES 10, 4
	IF FLAG 6 = 0
	THEN
		#TEXT __DWARF:_BEEEUUUURRRPPSSSSMF__
		TEXT __ENANO:_BEEEUUUURRRPPPSSSMF__
		SHOW
		SOUND 10
		SET FLAG 6 = 1
	END
END

# Talisman #3 (FLAG 12)

ENTERING SCREEN 22
	IF FLAG 12 = 0
	THEN
		HIDE_COINS
		SET FLAG 6 = 0
		SET TILE (7, 8) = 17
		#TEXT ____ONE_TALISMAN_PORTION!!____
		TEXT ___UNA_PIEZA_DEL_TALISMAN!!___
		SET_FIRE_ZONE 112, 128, 127, 143
	END
END

PRESS_FIRE AT SCREEN 22
	IF FLAG 12 = 0
	IF FLAG 6 = 0
	IF PLAYER_TOUCHES 7, 8
	THEN
		SET FLAG 1 = 0
		SET FLAG 6 = 1
		SET FLAG 12 = 1
		INC FLAG 2, 1
		INC OBJECTS 1
		SHOW_COINS
		ENABLE_TYPE_6
		REDRAW
		SET TILE (0, 2) = 16
		SET TILE (7, 8) = 0
		#TEXT __________TRAPPED!!___________
		TEXT __________ATRAPADO!!__________
		SHOW
		SOUND 6
	END
END

# Cripta del sirviente vicente

ENTERING SCREEN 2
	IF TRUE
	THEN
		SET TILE (9, 7) = 25
		#TEXT _____SERVANT_GRANT'S_CRYPT_____
		TEXT _CRIPTA_DEL_SIRVIENTE_VICENTE__
		SET_FIRE_ZONE 144, 112, 159, 127
		SET FLAG 6 = 0
	END
END

PRESS_FIRE AT SCREEN 2
	IF PLAYER_TOUCHES 9, 7
	IF FLAG 6 = 0
	THEN
		#TEXT __GRANT:_SLIPPERY_WHEN_WET!!__
		TEXT VICENTE:_NO_ME_PISES_LO_MOJAO!
		SHOW
		SOUND 7
		SET FLAG 6 = 1
	END
END

# Talisman #4 (FLAG 13)

ENTERING SCREEN 15
	IF FLAG 13 = 0
	THEN
		HIDE_COINS
		SET FLAG 6 = 0
		SET TILE (8, 8) = 17
		#TEXT ____ONE_TALISMAN_PORTION!!____
		TEXT ___UNA_PIEZA_DEL_TALISMAN!!___
		SET_FIRE_ZONE 128, 128, 143, 143
	END
END

PRESS_FIRE AT SCREEN 15
	IF FLAG 13 = 0
	IF FLAG 6 = 0
	IF PLAYER_TOUCHES 8, 8
	THEN
		SET FLAG 1 = 0
		SET FLAG 6 = 1
		SET FLAG 13 = 1
		INC FLAG 2, 1
		INC OBJECTS 1
		SHOW_COINS
		ENABLE_TYPE_6
		REDRAW
		SET TILE (14, 7) = 16
		SET TILE (8, 8) = 0
		#TEXT __________TRAPPED!!___________
		TEXT __________ATRAPADO!!__________
		SHOW
		SOUND 6
	END
END

# El Tesoro del Moro

ENTERING SCREEN 14
	# Sin el encantamiento del pimiento, puerta cerrada
	IF FLAG 4 = 0
	THEN
		SET TILE (10, 8) = 16
		#TEXT _____THE_TREASURE_CHAMBER_____
		TEXT _____LA_CAMARA_DEL_TESORO_____
	END
	
	IF FLAG 4 = 1
	IF FLAG 7 = 0
	THEN
		#TEXT ___NOW_THE_CHAMBER_IS_OPEN____
		TEXT _AHORA_LA_CAMARA_ESTA_ABIERTA_
		SET_FIRE_ZONE 176, 32, 191, 47
	END
	
	# Si no hemos cogido el tesoro
	IF FLAG 7 = 0
	THEN
		SET TILE (11, 2) = 28
	END
END

PRESS_FIRE AT SCREEN 14
	IF FLAG 7 = 0
	IF PLAYER_TOUCHES 11, 2
	THEN
		SET TILE (11, 2) = 0
		#TEXT AT_LAST!_THE_RAPTURE_TRASURE!_
		TEXT _POR_FIN!_EL_TESORO_DEL_MORO!_
		SOUND 6
		SOUND 6
		SOUND 6
		SOUND 6
		SOUND 6
		SOUND 6
		#TEXT _GOTTA_TAKE_IT_TO_MY_DAUGHTER_
		TEXT _VOY_A_LLEVARSELO_A_LA_CANIJA_
		SET FLAG 7 = 1
		PRINT_TILE_AT (0, 0) = 28
	END
END
