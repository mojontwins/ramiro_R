# El Vampiro Ramiro no tiene un respiro B
# Copyleft 2013 by The Mojon Twins.
# Churrera 4.6

# flags:
# 1	- Number of coins
# 2 - Number of talisman pieces
# 3 - Hechizo postizo
# 4 - Estado del juego. 0 = inicio, 1 = trampas activadas,
# 5 - Trampas activadas
# 6 - Onscreen check flag
# 7
# 10 - talisman 1  ( 0 = nada, 1 = aplastado personaje, 2 = puesto talisman)
# 11 - talisman 2
# 12 - talisman 3
# 13 - talisman 4

ENTERING GAME
	IF TRUE
	THEN
		SET FLAG 3 = 1		
		ENABLE_KILL_SLOWLY
		DISABLE_TYPE_6
		DISABLE_MAKE_TYPE_6
	END
END

ENTERING ANY
	IF FLAG 7 = 0
	IF FLAG 2 = 4
	THEN
		SET FLAG 7 = 1
		#TEXT __NOW_STUFF_WILL_BE_TOUGHER!__
		TEXT __EL_CASTILLO_SE_PONE_CHUNGO__
		ENABLE_MAKE_TYPE_6
		ENABLE_TYPE_6
		INC LIFE 10
	END
END

PLAYER_GETS_COIN
	IF TRUE
	THEN
		INC LIFE 1
	END
	
	IF FLAG 1 = 20
	THEN
		SOUND 8
		DISABLE_TYPE_6
		HIDE_COINS
		REDRAW
		#TEXT _______TRAP_DEACTIVATED_______
		TEXT ______TRAMPA_DESACTIVADA______
	END
	
	IF FLAG 1 = 20
	IF NPANT 29
	THEN
		SET TILE (10, 7) = 17
		SET TILE (10, 8) = 26
	END
	
	IF FLAG 1 = 20
	IF NPANT 18
	THEN
		SET TILE (5, 7) = 18
		SET TILE (5, 8) = 26
	END
	
	IF FLAG 1 = 20
	IF NPANT 13
	THEN
		SET TILE (5, 1) = 19
		SET TILE (5, 2) = 26
	END
	
	IF FLAG 1 = 20
	IF NPANT 5
	THEN
		SET TILE (5, 5) = 20
		SET TILE (5, 6) = 26
	END
END

# Pantalla inicial

ENTERING SCREEN 25
	IF TRUE
	THEN
		SET TILE (4, 4) = 25
		SET_FIRE_ZONE 40, 64, 64, 79
		SET FLAG 6 = 0
	END
END

PRESS_FIRE AT SCREEN 25
	IF FLAG 4 = 0 
	IF FLAG 6 = 0
	IF PLAYER_TOUCHES 3, 4
	THEN
		SET FLAG 6 = 1
		EXTERN 1
		REDRAW
		SET TILE (4, 4) = 25
	END
	
	IF FLAG 4 <> 0
	IF FLAG 6 = 0
	IF PLAYER_TOUCHES 3, 4
	THEN
		#TEXT _WITCH:_YOU_HAVE_THINGS_TO_DO_	
		TEXT __BRUJA:_NO_TE_ENTRETENGAS!!__	
		SHOW
		SOUND 7
		SET FLAG 6 = 1
	END
END

####################
# RUFINO EL MARINO #
####################

ENTERING SCREEN 28
	IF FLAG 5 = 1
	THEN
		SET TILE (12, 5) = 29
	END

	IF FLAG 10 = 0 
	THEN
		SET TILE (11, 7) = 13
		SET TILE (11, 8) = 21
		#TEXT ____JIM_TAYLOR_THE_SAILOR_____
		TEXT ______RUFINO_EL_MARINO________
		SET_FIRE_ZONE 160, 112, 175, 143
		SET FLAG 6 = 0
	END		
END

PRESS_FIRE AT SCREEN 28
	IF FLAG 5 = 1
	IF FLAG 10 = 0
	IF PLAYER_TOUCHES 12, 5
	THEN
		SOUND 8
		SET TILE (11, 7) = 28
		SHOW
		SOUND 3
		SOUND 4
		SET TILE (11, 7) = 27
		SET TILE (11, 8) = 28
		SHOW
		SOUND 10
		SET TILE (11, 7) = 28
		SET TILE (11, 8) = 0
		SHOW
		SOUND 3
		SOUND 4
		SET TILE (11, 7) = 0
		SHOW
		SOUND 3
		SOUND 4
		SET FLAG 10 = 1
	END
	
	IF FLAG 10 = 0
	IF FLAG 6 = 0
	IF PLAYER_TOUCHES 10, 8
	THEN
		#TEXT _JIM:_HAR_HAR!_ASK_GIGATRON!!_
		TEXT __RUFINO:_ME_CABE_UN_BARCO!___
		SOUND 11
		SET FLAG 6 = 1
	END
END

############
# CRIPTA 1 #
############

ENTERING SCREEN 29
	IF TRUE 
	THEN
		SET TILE (10, 8) = 26
		HIDE_COINS
	END
	
	IF FLAG 10 = 2
	THEN
		SET TILE (10, 7) = 17
	END
	
	IF FLAG 10 = 1
	THEN
	    #TEXT PUT_THE_TALISMAN_IN_THE_SHRINE
		TEXT _PON_EL_TALISMAN_EN_EL_ALTAR!_
	END
END

PRESS_FIRE AT SCREEN 29
	IF PLAYER_TOUCHES 10, 8
	IF FLAG 10 = 1
	THEN
		SHOW_COINS
		ENABLE_TYPE_6
		REDRAW		
		SET FLAG 1 = 0
		SET FLAG 10 = 2
		SET TILE (10, 7) = 17
		SET TILE (10, 8) = 26
		SET TILE (0, 8) = 16
		INC FLAG 2, 1
		INC OBJECTS 1
		#TEXT __________TRAPPED!!___________
		TEXT __________ATRAPADO!!__________
		SHOW
		SOUND 6
	END
END

###########################
# FRITZ, EL BANCARIO ARIO #
###########################

ENTERING SCREEN 17
	IF FLAG 5 = 1
	THEN
		SET TILE (9, 2) = 29
	END

	IF FLAG 11 = 0 
	THEN
		SET TILE (11, 7) = 13
		SET TILE (11, 8) = 22
		#TEXT ____JIM_TAYLOR_THE_SAILOR_____
		TEXT ____FRITZ_EL_BANCARIO_ARIO____
		SET_FIRE_ZONE 160, 112, 175, 143
		SET FLAG 6 = 0
	END		
END

PRESS_FIRE AT SCREEN 17
	IF FLAG 5 = 1
	IF FLAG 11 = 0
	IF PLAYER_TOUCHES 9, 2
	THEN
		SOUND 8
		SET TILE (11, 7) = 28
		SHOW
		SOUND 3
		SOUND 4
		SET TILE (11, 7) = 27
		SET TILE (11, 8) = 28
		SHOW
		SOUND 10
		SET TILE (11, 7) = 28
		SET TILE (11, 8) = 0
		SHOW
		SOUND 3
		SOUND 4
		SET TILE (11, 7) = 0
		SHOW
		SOUND 3
		SOUND 4
		SET FLAG 11 = 1
	END
	
	IF FLAG 11 = 0
	IF FLAG 6 = 0
	IF PLAYER_TOUCHES 10, 8
	THEN
		#TEXT _FRITZ:_VENDO_MOTO_SEMINUEVA__
		TEXT _FRITZ:_VENDO_MOTO_SEMINUEVA__
		SOUND 7
		SET FLAG 6 = 1
	END
END

############
# CRIPTA 2 #
############

ENTERING SCREEN 18
	IF TRUE 
	THEN
		SET TILE (5, 8) = 26
		HIDE_COINS
	END
	
	IF FLAG 11 = 2
	THEN
		SET TILE (5, 7) = 18
	END
	
	IF FLAG 11 = 1
	THEN
	    #TEXT PUT_THE_TALISMAN_IN_THE_SHRINE
		TEXT _PON_EL_TALISMAN_EN_EL_ALTAR!_
	END
END

PRESS_FIRE AT SCREEN 18
	IF PLAYER_TOUCHES 5, 8
	IF FLAG 11 = 1
	THEN
		SHOW_COINS
		ENABLE_TYPE_6
		REDRAW		
		SET FLAG 1 = 0
		SET FLAG 11 = 2
		SET TILE (5, 7) = 18
		SET TILE (5, 8) = 26
		SET TILE (0, 1) = 16
		INC FLAG 2, 1
		INC OBJECTS 1
		#TEXT __________TRAPPED!!___________
		TEXT __________ATRAPADO!!__________
		SHOW
		SOUND 6
	END
END

######################
# ROBERTA, LA MUERTA #
######################

ENTERING SCREEN 12
	IF FLAG 5 = 1
	THEN
		SET TILE (13, 1) = 29
	END

	IF FLAG 12 = 0 
	THEN
		SET TILE (13, 3) = 13
		SET TILE (13, 4) = 23
		#TEXT ____RAIDEN_THE_DEAD_MAIDEN____
		TEXT __ROBERTA_LA_DONCELLA_MUERTA__
		SET_FIRE_ZONE 192, 64, 207, 79
		SET FLAG 6 = 0
	END		
END

PRESS_FIRE AT SCREEN 12
	IF FLAG 5 = 1
	IF FLAG 12 = 0
	IF PLAYER_TOUCHES 13, 1
	THEN
		SOUND 8
		SET TILE (13, 3) = 28
		SHOW
		SOUND 3
		SOUND 4
		SET TILE (13, 3) = 27
		SET TILE (13, 4) = 28
		SHOW
		SOUND 10
		SET TILE (13, 3) = 28
		SET TILE (13, 4) = 0
		SHOW
		SOUND 3
		SOUND 4
		SET TILE (13, 3) = 0
		SHOW
		SOUND 3
		SOUND 4
		SET FLAG 12 = 1
	END
	
	IF FLAG 12 = 0
	IF FLAG 6 = 0
	IF PLAYER_TOUCHES 12, 4
	THEN
		#TEXT _RAIDEN:_FANCY_SOME_MAGGOTS?__
		TEXT _ROBERTA:_QUIERES_GUSANITOS?__
		SOUND 7
		SET FLAG 6 = 1
	END
END

############
# CRIPTA 3 #
############

ENTERING SCREEN 13
	IF TRUE 
	THEN
		SET TILE (5, 2) = 26
		HIDE_COINS
	END
	
	IF FLAG 12 = 2
	THEN
		SET TILE (5, 1) = 19
	END
	
	IF FLAG 12 = 1
	THEN
	    #TEXT PUT_THE_TALISMAN_IN_THE_SHRINE
		TEXT _PON_EL_TALISMAN_EN_EL_ALTAR!_
	END
END

PRESS_FIRE AT SCREEN 13
	IF PLAYER_TOUCHES 5, 2
	IF FLAG 12 = 1
	THEN
		SHOW_COINS
		ENABLE_TYPE_6
		REDRAW		
		SET FLAG 1 = 0
		SET FLAG 12 = 2
		SET TILE (5, 1) = 19
		SET TILE (5, 2) = 26
		SET TILE (0, 4) = 16
		INC FLAG 2, 1
		INC OBJECTS 1
		#TEXT __________TRAPPED!!___________
		TEXT __________ATRAPADO!!__________
		SHOW
		SOUND 6
	END
END

##########################
# EL DEFORME DE UNIFORME #
##########################

ENTERING SCREEN 6
	IF FLAG 5 = 1
	THEN
		SET TILE (5, 8) = 29
	END

	IF FLAG 13 = 0 
	THEN
		SET TILE (4, 1) = 13
		SET TILE (4, 2) = 24
		#TEXT ____RAIDEN_THE_DEAD_MAIDEN____
		TEXT ____EL_DEFORME_DE_UNIFORME____
		SET_FIRE_ZONE 80, 32, 95, 47
		SET FLAG 6 = 0
	END		
END

PRESS_FIRE AT SCREEN 6
	IF FLAG 5 = 1
	IF FLAG 13 = 0
	IF PLAYER_TOUCHES 5, 8
	THEN
		SOUND 8
		SET TILE (4, 1) = 28
		SHOW
		SOUND 3
		SOUND 4
		SET TILE (4, 1) = 27
		SET TILE (4, 2) = 28
		SHOW
		SOUND 10
		SET TILE (4, 1) = 28
		SET TILE (4, 2) = 0
		SHOW
		SOUND 3
		SOUND 4
		SET TILE (4, 1) = 0
		SHOW
		SOUND 3
		SOUND 4
		SET FLAG 13 = 1
	END
	
	IF FLAG 13 = 0
	IF FLAG 6 = 0
	IF PLAYER_TOUCHES 5, 2
	THEN
		#TEXT _RAIDEN:_FANCY_SOME_MAGGOTS?__
		TEXT _DEFORME:_NO_GANO_PARA_TRAJES_
		SOUND 7
		SET FLAG 6 = 1
	END
END

############
# CRIPTA 4 #
############

ENTERING SCREEN 5
	IF TRUE 
	THEN
		SET TILE (5, 6) = 26
		HIDE_COINS
	END
	
	IF FLAG 13 = 2
	THEN
		SET TILE (5, 5) = 20
	END
	
	IF FLAG 13 = 1
	THEN
	    #TEXT PUT_THE_TALISMAN_IN_THE_SHRINE
		TEXT _PON_EL_TALISMAN_EN_EL_ALTAR!_
	END
END

PRESS_FIRE AT SCREEN 5
	IF PLAYER_TOUCHES 5, 6
	IF FLAG 13 = 1
	THEN
		SHOW_COINS
		ENABLE_TYPE_6
		REDRAW		
		SET FLAG 1 = 0
		SET FLAG 13 = 2
		SET TILE (5, 5) = 20
		SET TILE (5, 6) = 26
		SET TILE (14, 6) = 16
		INC FLAG 2, 1
		INC OBJECTS 1
		#TEXT __________TRAPPED!!___________
		TEXT __________ATRAPADO!!__________
		SHOW
		SOUND 6
	END
END

###################
# GONZALO EL MALO #
###################

ENTERING SCREEN 2
	IF TRUE
	THEN
		SET TILE (4, 7) = 30
		SET TILE (4, 8) = 31
		#TEXT __WEEVIL_THE_EVIL'S_CHAMBER___
		TEXT _LA_CAMARA_DE_GONZALO_EL_MALO_
		SET FLAG 6 = 0
		SET_FIRE_ZONE 0, 48, 112, 255
	END
END

PRESS_FIRE AT SCREEN 2
	IF PLAYER_IN_X 48, 96
	IF FLAG 4 = 0
	IF FLAG 6 = 0
	THEN
		SET FLAG 4 = 1
		SET FLAG 5 = 1
		SET FLAG 6 = 1
		EXTERN 2
		EXTERN 3
		REDRAW
		SET TILE (4, 7) = 30
		SET TILE (4, 8) = 31
	END
	
	IF PLAYER_IN_X 48, 96
	IF FLAG 4 = 1
	IF FLAG 6 = 0
	IF FLAG 2 <> 4
	THEN
		SET FLAG 6 = 1
		#TEXT _WEEVIL:_MORE_WORK_TO_BE_DONE_
		TEXT _GONZALO:_AUN_NO_ESTAN_TODOS!_
		SHOW
		SOUND 7
	END
	
	IF PLAYER_IN_X 48, 96
	IF FLAG 4 = 1
	IF FLAG 2 = 4
	THEN
		EXTERN 4
		WIN GAME
	END
END
